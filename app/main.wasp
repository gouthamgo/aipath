app AiPath {
  wasp: {
    version: "^0.19.0"
  },

  title: "AI Path - Learn AI Engineering",

  head: [
    "<link rel='icon' href='/favicon.ico' />",
    "<meta charset='utf-8' />",
    "<meta name='description' content='Master AI Engineering through hands-on Python projects. Learn LangChain, build AI agents, and deploy production systems.' />",
    "<meta name='author' content='AI Path' />",
    "<meta name='keywords' content='ai engineering, python, langchain, machine learning, llm, ai agents, rag' />",

    "<meta property='og:type' content='website' />",
    "<meta property='og:title' content='AI Path - Learn AI Engineering' />",
    "<meta property='og:site_name' content='AI Path' />",
    "<meta property='og:url' content='https://aipath.dev' />",
    "<meta property='og:description' content='Master AI Engineering through hands-on Python projects. Learn LangChain, build AI agents, and deploy production systems.' />",
    "<meta property='og:image' content='https://aipath.dev/public-banner.webp' />",
    "<meta name='twitter:image' content='https://aipath.dev/public-banner.webp' />",
    "<meta name='twitter:image:width' content='800' />",
    "<meta name='twitter:image:height' content='400' />",
    "<meta name='twitter:card' content='summary_large_image' />",
    // Google Fonts - JetBrains Mono for code, Plus Jakarta Sans for UI
    "<link rel='preconnect' href='https://fonts.googleapis.com' />",
    "<link rel='preconnect' href='https://fonts.gstatic.com' crossorigin />",
    "<link href='https://fonts.googleapis.com/css2?family=JetBrains+Mono:wght@400;500;600;700&family=Plus+Jakarta+Sans:wght@400;500;600;700;800&family=Space+Grotesk:wght@400;500;600;700&display=swap' rel='stylesheet' />",
  ],

  // üîê Auth out of the box! https://wasp.sh/docs/auth/overview
  auth: {
    userEntity: User,
    methods: {
      email: {
        fromField: {
          name: "AI Path",
          email: "hello@aipath.dev"
        },
        emailVerification: {
          clientRoute: EmailVerificationRoute,
          getEmailContentFn: import { getVerificationEmailContent } from "@src/auth/email-and-pass/emails",
        },
        passwordReset: {
          clientRoute: PasswordResetRoute,
          getEmailContentFn: import { getPasswordResetEmailContent } from "@src/auth/email-and-pass/emails",
        },
        userSignupFields: import { getEmailUserFields } from "@src/auth/userSignupFields",
      },
      // Uncomment to enable Google Auth:
      // google: {
      //   userSignupFields: import { getGoogleUserFields } from "@src/auth/userSignupFields",
      //   configFn: import { getGoogleAuthConfig } from "@src/auth/userSignupFields",
      // },
      // Uncomment to enable GitHub Auth:
      // gitHub: {
      //   userSignupFields: import { getGitHubUserFields } from "@src/auth/userSignupFields",
      //   configFn: import { getGitHubAuthConfig } from "@src/auth/userSignupFields",
      // },
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/onboarding",
  },

  db: {
    // Run `wasp db seed` to seed the database with the seed functions below:
    seeds: [
      // Populates the database with a bunch of fake users to work with during development.
      import { seedMockUsers } from "@src/server/scripts/dbSeeds",
    ]
  },

  client: {
    rootComponent: import App from "@src/client/App",
  },

  emailSender: {
    // NOTE: "Dummy" provider is just for local development purposes.
    //   Make sure to check the server logs for the email confirmation url (it will not be sent to an address)!
    //   Once you are ready for production, switch to e.g. "SendGrid" or "Mailgun" providers. Check out https://docs.opensaas.sh/guides/email-sending/ .
    provider: Dummy,
    defaultFrom: {
      name: "Open SaaS App",
      // When using a real provider, e.g. SendGrid, you must use the same email address that you configured your account to send out emails with!
      email: "me@example.com"
    },
  },
}

route LandingPageRoute { path: "/", to: LandingPage }
page LandingPage {
  component: import LandingPage from "@src/landing-page/LandingPage"
}

//#region Auth Pages
route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@src/auth/LoginPage"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import { Signup } from "@src/auth/SignupPage"
}

route RequestPasswordResetRoute { path: "/request-password-reset", to: RequestPasswordResetPage }
page RequestPasswordResetPage {
  component: import { RequestPasswordResetPage } from "@src/auth/email-and-pass/RequestPasswordResetPage",
}

route PasswordResetRoute { path: "/password-reset", to: PasswordResetPage }
page PasswordResetPage {
  component: import { PasswordResetPage } from "@src/auth/email-and-pass/PasswordResetPage",
}

route EmailVerificationRoute { path: "/email-verification", to: EmailVerificationPage }
page EmailVerificationPage {
  component: import { EmailVerificationPage } from "@src/auth/email-and-pass/EmailVerificationPage",
}
//#endregion

//#region User
route AccountRoute { path: "/account", to: AccountPage }
page AccountPage {
  authRequired: true,
  component: import Account from "@src/user/AccountPage"
}

query getPaginatedUsers {
  fn: import { getPaginatedUsers } from "@src/user/operations",
  entities: [User]
}

action updateIsUserAdminById {
  fn: import { updateIsUserAdminById } from "@src/user/operations",
  entities: [User]
}

action exportUserData {
  fn: import { exportUserData } from "@src/user/operations",
  entities: [User, UserProgress, CodeSubmission, File, ContactFormMessage, Task, GptResponse]
}

action deleteUserAccount {
  fn: import { deleteUserAccount } from "@src/user/operations",
  entities: [User, UserProgress, CodeSubmission, File, ContactFormMessage, Task, GptResponse]
}
//#endregion

//#region Learning Platform
route OnboardingRoute { path: "/onboarding", to: OnboardingPage }
page OnboardingPage {
  authRequired: true,
  component: import OnboardingPage from "@src/learning/pages/OnboardingPage"
}

route DashboardRoute { path: "/dashboard", to: DashboardPage }
page DashboardPage {
  authRequired: true,
  component: import DashboardPage from "@src/learning/pages/DashboardPage"
}

route ProjectsRoute { path: "/projects", to: ProjectsPage }
page ProjectsPage {
  authRequired: true,
  component: import ProjectsPage from "@src/learning/pages/ProjectsPage"
}

route ProjectDetailRoute { path: "/projects/:projectSlug", to: ProjectDetailPage }
page ProjectDetailPage {
  authRequired: true,
  component: import ProjectDetailPage from "@src/learning/pages/ProjectDetailPage"
}

route LessonRoute { path: "/projects/:projectSlug/lessons/:lessonSlug", to: LessonPage }
page LessonPage {
  authRequired: true,
  component: import LessonPage from "@src/learning/pages/LessonPage"
}

// Learning Queries
query getProjects {
  fn: import { getProjects } from "@src/learning/operations",
  entities: [Project, Lesson, UserProgress]
}

query getProject {
  fn: import { getProject } from "@src/learning/operations",
  entities: [Project, Lesson, UserProgress]
}

query getLesson {
  fn: import { getLesson } from "@src/learning/operations",
  entities: [Lesson, UserProgress, Project, User]
}

query getDashboardData {
  fn: import { getDashboardData } from "@src/learning/operations",
  entities: [User, Project, Lesson, UserProgress]
}

query getUserProgress {
  fn: import { getUserProgress } from "@src/learning/operations",
  entities: [UserProgress, Lesson, Project]
}

// Learning Actions
action executePythonCode {
  fn: import { executePythonCode } from "@src/learning/operations",
  entities: [CodeSubmission, User]
}

action saveUserCode {
  fn: import { saveUserCode } from "@src/learning/operations",
  entities: [UserProgress]
}

action markLessonComplete {
  fn: import { markLessonComplete } from "@src/learning/operations",
  entities: [UserProgress, User]
}

action updateUserApiKeys {
  fn: import { updateUserApiKeys } from "@src/learning/operations",
  entities: [User]
}
//#endregion

//#region Payment
route PricingPageRoute { path: "/pricing", to: PricingPage }
page PricingPage {
  component: import PricingPage from "@src/payment/PricingPage"
}

route CheckoutResultRoute { path: "/checkout", to: CheckoutResultPage }
page CheckoutResultPage {
  authRequired: true,
  component: import CheckoutResultPage from "@src/payment/CheckoutResultPage"
}

query getCustomerPortalUrl {
  fn: import { getCustomerPortalUrl } from  "@src/payment/operations",
  entities: [User]
}

action generateCheckoutSession {
  fn: import { generateCheckoutSession } from "@src/payment/operations",
  entities: [User]
}

api paymentsWebhook {
  fn: import { paymentsWebhook } from "@src/payment/webhook",
  entities: [User],
  middlewareConfigFn: import { paymentsMiddlewareConfigFn } from "@src/payment/webhook",
  httpRoute: (POST, "/payments-webhook")
}
//#endregion

//#region File Upload
route FileUploadRoute { path: "/file-upload", to: FileUploadPage }
page FileUploadPage {
  authRequired: true,
  component: import FileUpload from "@src/file-upload/FileUploadPage"
}

action createFileUploadUrl {
  fn: import { createFileUploadUrl } from "@src/file-upload/operations",
  entities: [User, File]
}

action addFileToDb {
  fn: import { addFileToDb } from "@src/file-upload/operations",
  entities: [User, File]
}

query getAllFilesByUser {
  fn: import { getAllFilesByUser } from "@src/file-upload/operations",
  entities: [User, File]
}

query getDownloadFileSignedURL {
  fn: import { getDownloadFileSignedURL } from "@src/file-upload/operations",
  entities: [User, File]
}

action deleteFile {
  fn: import { deleteFile } from "@src/file-upload/operations",
  entities: [User, File]
}
//#endregion

//#region Analytics
query getDailyStats {
  fn: import { getDailyStats } from "@src/analytics/operations",
  entities: [User, DailyStats]
}

job dailyStatsJob {
  executor: PgBoss,
  perform: {
    fn: import { calculateDailyStats } from "@src/analytics/stats"
  },
  schedule: {
    cron: "0 * * * *" // every hour. useful in production
    // cron: "* * * * *" // every minute. useful for debugging
  },
  entities: [User, DailyStats, Logs, PageViewSource]
}
//#endregion

//#region Admin Dashboard
route AdminRoute { path: "/admin", to: AnalyticsDashboardPage }
page AnalyticsDashboardPage {
  authRequired: true,
  component: import AnalyticsDashboardPage from "@src/admin/dashboards/analytics/AnalyticsDashboardPage"
}

route AdminUsersRoute { path: "/admin/users", to: AdminUsersPage }
page AdminUsersPage {
  authRequired: true,
  component: import AdminUsers from "@src/admin/dashboards/users/UsersDashboardPage"
}

route AdminSettingsRoute { path: "/admin/settings", to: AdminSettingsPage }
page AdminSettingsPage {
  authRequired: true,
  component: import AdminSettings from "@src/admin/elements/settings/SettingsPage"
}

route AdminCalendarRoute { path: "/admin/calendar", to: AdminCalendarPage }
page AdminCalendarPage {
  authRequired: true,
  component: import AdminCalendar from "@src/admin/elements/calendar/CalendarPage"
}


route AdminUIButtonsRoute { path: "/admin/ui/buttons", to: AdminUIButtonsPage }
page AdminUIButtonsPage {
  authRequired: true,
  component: import AdminUI from "@src/admin/elements/ui-elements/ButtonsPage"
}

route NotFoundRoute { path: "*", to: NotFoundPage }
page NotFoundPage {
  component: import { NotFoundPage } from "@src/client/components/NotFoundPage"
}
//#endregion

//#region Legal Pages
route PrivacyPolicyRoute { path: "/privacy-policy", to: PrivacyPolicyPage }
page PrivacyPolicyPage {
  component: import PrivacyPolicyPage from "@src/legal/PrivacyPolicyPage"
}

route TermsOfServiceRoute { path: "/terms-of-service", to: TermsOfServicePage }
page TermsOfServicePage {
  component: import TermsOfServicePage from "@src/legal/TermsOfServicePage"
}
//#endregion

//#region Contact Form Messages
// TODO:
// add functionality to allow users to send messages to admin
// and make them accessible via the admin dashboard
route AdminMessagesRoute { path: "/admin/messages", to: AdminMessagesPage }
page AdminMessagesPage {
  authRequired: true,
  component: import AdminMessages from "@src/admin/dashboards/messages/MessagesPage"
}
//#endregion
